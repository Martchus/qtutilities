_name=qtutilities
pkgname=mingw-w64-$_name
pkgver=1.0.4
pkgrel=1
arch=('any')
pkgdesc="Collection of Qt related utility classes and functions used by Tageditor, Video downloader and Password manager (mingw-w64)."
license=('GPL')
depends=('mingw-w64-crt' 'mingw-w64-qt5-base' 'mingw-w64-c++utilities')
makedepends=('mingw-w64-gcc')
url="http://martchus.netai.net/"
source=("${_name}.tar.gz")
md5sums=('4e94aff9225d8873f752995c1bcc5f15')
options=('staticlibs' '!strip')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"
# head end
build() {
    cd $srcdir/$_name
    # build utilities for each architecture
    for _arch in ${_architectures}; do
        mkdir -p build-${_arch} && pushd build-${_arch}
        INSTALL_ROOT=$pkgdir/usr/ ${_arch}-qmake-qt5 -r ../${_name}.pro
        make
        popd
    done
}

package() {
    cd $srcdir/$_name
    for _arch in ${_architectures}; do       
        # bin stuff
        pushd build-${_arch}
        ${_arch}-strip --strip-unneeded ./release/${_name}.dll
        ${_arch}-strip --strip-unneeded ./release/lib${_name}.dll.a
        install -m755 -D ./release/${_name}.dll $pkgdir/usr/${_arch}/bin/${_name}.dll
        install -m755 -D ./release/lib${_name}.dll.a $pkgdir/usr/${_arch}/lib/lib${_name}.dll.a
        popd          
        # include files
        for dir in aboutdialog enterpassworddialog settingsdialog widgets resources models
        do
            mkdir -p $pkgdir/usr/${_arch}/include/${_name}/$dir
            install -m644 -D ./$dir/*.h $pkgdir/usr/${_arch}/include/${_name}/$dir
        done    
    done
}
